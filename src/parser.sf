
-- Functions for primitive types
let charEq (a : Char) (b : Char) : Bool = a.equals(b);
let intEq (a : Int) (b : Int) : Bool = a.equals(b);
let boolEq (a : Bool) (b : Bool) : Bool = a.equals(b);
let unitEq (a : Unit) (b : Unit) : Bool = a == b;

let charToString (a : Char) : String = a.toString();
let intToString (a : Int) : String = a.toString();
let boolToString (a : Bool) : String = a.toString();
let unitToString (a : Unit) : String = "()";

-- Definition of Thunk
type Thunk[A] = Unit -> A;

let invoke[A] (t : Thunk[A]) : A =
    t ();

-- Maybe definition

data Maybe[A] = Nothing
              | Just A
              ;

-- Result definition
data Result[S, E] = Ok S
                  | Err E
                  ;

-- Eq definition
type Eq[A] = A -> A -> Bool;

-- State definition
type State[S, A] = S -> (A, S);

-- ADT List
data PolyList[A] = Nil
                 | Cons A (Thunk[PolyList[A]])
                 ;

let rec nats (beg : Int) : PolyList[Int] =
    Cons[Int] beg (\(__ : Unit) -> nats (beg+1));

-- Utilities

let singleton[A] (x : A) : PolyList[A] =
    Cons[A] x (\(__: Unit) -> (Nil[A]));

let rec map[A, B] (f : A -> B) (l : PolyList[A]) : PolyList[B] =
    case l of
        Nil             ->      Nil[B]
     |  Cons x xs       ->      Cons[B] (f x) (\(__ : Unit) -> (map[A, B] f (invoke[PolyList[A]] xs)));

let rec foldl[A, B] (f : B -> A -> B) (z : B) (l : PolyList[A]) : B =
    case l of
        Nil             ->      z
     |  Cons x xs       ->      foldl[A, B] f (f z x) (invoke[PolyList[A]] xs);

--let rec foldr[A, B] (f : A -> B -> B) (z : B) (l : PolyList[A]) : B =
--    case l of
--        Nil             ->      z
--     |  Cons x xs       ->      f x (foldr[A, B] f z (invoke[PolyList[A]] xs));

let foldr[A, B] (f : A -> B -> B) (z : B) (l : PolyList[A]) : B =
    (let rec inner (s : PolyList[A]) (k : B -> B) : B =
        case s of
            Nil         -> k z
         |  Cons x xs   -> inner (invoke[PolyList[A]] xs) (\(y : B) -> k (f x y));
     inner l (\(x : B) -> x));


let join[A] (l : PolyList[A]) (j : A) : PolyList[A] = {
    let rec inner[A] (l : PolyList[A]) (j : A) : PolyList[A] =
        case l of
            Nil         -> (Nil[A])
         |  Cons x xs   -> (Cons[A] j (\(__ : Unit) ->
                                (Cons[A] x (\(__ : Unit) -> inner[A] (invoke[PolyList[A]] xs) j))));
    case l of
        Nil         -> (Nil[A])
     |  Cons x xs   -> (Cons[A] x (\(__ : Unit) -> inner[A] (invoke[PolyList[A]] xs) j))
};

let (+>)[A] (a : A) (l : PolyList[A]) : PolyList[A] =
    Cons[A] a (\(__ : Unit) -> l);

let rec concat2[A] (a : PolyList[A]) (b : PolyList[A]) : PolyList[A] =
    case a of
        Nil         ->  b
      | Cons x xs   ->  Cons[A] x (\(__ : Unit) -> (concat2[A] (invoke[PolyList[A]] xs) b));

let rec lazyconcat2[A] (a : PolyList[A]) (b : Thunk[PolyList[A]]) : PolyList[A] =
    case a of
        Nil         ->  (invoke[PolyList[A]] b)
      | Cons x xs   ->  Cons[A] x (\(__ : Unit) -> (lazyconcat2[A] (invoke[PolyList[A]] xs) b));

let (++)[A] (a : PolyList[A]) (b : PolyList[A]) : PolyList[A] =
    concat2[A] a b;

let (+~)[A] (a : PolyList[A]) (b : Thunk[PolyList[A]]) : PolyList[A] =
    lazyconcat2[A] a b;

let rec concat[A] (l : PolyList[PolyList[A]]) : PolyList[A] =
    case l of
        Nil         ->  Nil[A]
     |  Cons x xs   ->  x +~[A] (\(__ : Unit) -> (concat[A] (invoke[PolyList[PolyList[A]]] xs)));

let rec len[A] (l : PolyList[A]) : Int =
    case l of
        Nil         ->  0
     |  Cons _ xs   ->  1 + (len[A] (invoke[PolyList[A]] xs));

let isempty[A] (l : PolyList[A]) : Bool =
    case l of
        Nil         -> True
     |  Cons _ _    -> False;

let first[A] (l : PolyList[A]) : Maybe[A] =
    case l of
        Nil         -> Nothing[A]
     |  Cons x _    -> Just[A] x;

let rest[A] (l : PolyList[A]) : Maybe[PolyList[A]] =
    case l of
        Nil         -> Nothing[PolyList[A]]
     |  Cons _ xs   -> Just[PolyList[A]] (invoke[PolyList[A]] xs);

let rec take[A] (n : Int) (l : PolyList[A]) : PolyList[A] =
    if n <= 0 then
        (Nil[A])
    else
        case l of
            Nil         ->  Nil[A]
         |  Cons x xs   ->  Cons[A] x (\(__ : Unit) -> (take[A] (n-1) (invoke[PolyList[A]] xs)));

let rec zip[A, B] (a : PolyList[A]) (b : PolyList[B]) : PolyList[(A, B)] =
    case a of
        Nil         -> Nil[(A, B)]
     |  Cons xa xsa ->
            case b of
                Nil         -> Nil[(A, B)]
             |  Cons xb xsb ->
                    Cons[(A, B)] (xa, xb) (\(__ : Unit) -> zip[A, B] (invoke[PolyList[A]] xsa) (invoke[PolyList[B]] xsb));

let listToString[A] (tostr : A -> String) (l : PolyList[A]) : String = {
    type StringBuilder = java.lang.StringBuilder;

    let inner = foldl[String, StringBuilder]
                    (\(b : StringBuilder) (a : String) -> b.append(a))
                    (new java.lang.StringBuilder("["))
                    (join[String] (map[A, String] tostr l) ", ");

    inner.append("]").toString()
};

let rec polyListEq[A] (eq : Eq[A]) (a : PolyList[A]) (b : PolyList[A]) : Bool =
    case a of
        Nil         -> { case b of Nil -> True | Cons _ _ -> False }
     |  Cons ax axs ->
            case b of
                Nil         -> False
             |  Cons bx bxs -> if eq ax bx then
                                    polyListEq[A] eq (invoke[PolyList[A]] axs) (invoke[PolyList[A]] bxs)
                               else False;

-- ADT String

type PolyString = PolyList[Char];

let toString (l : PolyString) : String =
    (foldl[Char, java.lang.StringBuilder]
        (\(b : java.lang.StringBuilder) (a : Char) -> b.append(a))
        (new java.lang.StringBuilder( ))
        l).toString();

let fromString (str : String) : PolyString = {
    let rec inner (str : String) (idx : Int) : PolyString =
        if idx `intEq` length(str) then
            (Nil[Char])
        else
            (Cons[Char] str.charAt(idx) (\(__ : Unit) -> (inner str (idx+1))));
    inner str 0
};

let rec polyStringEq (a : PolyString) (b : PolyString) : Bool =
    polyListEq[Char] (\(a : Char) (b : Char) -> a.equals(b)) a b;

let rec charin (c : Char) (str : PolyString) : Bool =
    case str of
        Nil         ->      False
     |  Cons x xs   ->      if x.equals(c) then True else (charin c (invoke[PolyString] xs));

-- Parser type definition

type Parser[A] = PolyString -> PolyList[(A, PolyString)];

let parseResultEq[A] (eq : Eq[A]) (a : (A, PolyString)) (b : (A, PolyString)): Bool =
    (a._1 `eq` b._1) && (a._2 `polyStringEq` b._2);

let parseResultListEq[A] (eq : Eq[A]) (a : PolyList[(A, PolyString)]) (b : PolyList[(A, PolyString)]) : Bool =
    polyListEq[(A, PolyString)]
        (parseResultEq[A] eq)
        a b;

let parseResultToString[A] (str : A -> String) (res : (A, PolyString)) : String =
    (new java.lang.StringBuilder("("))
        .append(str res._1)
        .append(" ,\"")
        .append(toString res._2)
        .append("\")")
        .toString();

let parseResultListToString[A] (str : A -> String) (res : PolyList[(A, PolyString)]) : String =
    listToString[(A, PolyString)]
        (parseResultToString[A] str)
        res;

-- Primitive Parsers

let result[V] (value : V) : Parser[V] =
    \(inp : PolyString) -> singleton[(V, PolyString)] (value, inp);

let zero[V] : Parser[V] =
    \(inp : PolyString) -> Nil[(V, PolyString)];

let item : Parser[Char] =
    \(inp : PolyString) ->
        case inp of
            Nil         ->      Nil[(Char, PolyString)]
          | Cons c xs   ->      singleton[(Char, PolyString)] (c, (invoke[PolyString] xs));

-- Combinators

let bind[A, B] (p : Parser[A]) (f : A -> Parser[B]) : Parser[B] =
    \(inp : PolyString) ->
        concat[(B, PolyString)]
              (map[(A, PolyString), PolyList[(B, PolyString)]]
                  (\(v : (A, PolyString)) -> f v._1 v._2) (p inp));

let (>>=)[A, B] (p : Parser[A]) (f : A -> Parser[B]) : Parser[B] =
    bind[A, B] p f;

let (>>)[A, B] (p : Parser[A]) (q : Parser[B]) : Parser[B] =
    p >>=[A, B] (\(__ : A) -> q);

let (<*)[A, B] (p : Parser[A]) (q : Parser[B]) : Parser[A] =
    p >>=[A, A] (\(a : A) ->
    q >>=[B, A] (\(__ : B) ->
    result[A] a));

let (*>)[A, B] (p : Parser[A]) (q : Parser[B]) : Parser[B] =
    p >>[A, B] q;

let using[A, B] (p : Parser[A]) (f : A -> B) : Parser[B] =
    bind[A, B] p (\(a : A) -> result[B] (f a));

let (<$>)[A, B] (p : Parser[A]) (f : A -> B) : Parser[B] =
    using[A, B] p f;

let ($>)[A, B] (p : Parser[A]) (b : B) : Parser[B] =
    using[A, B] p (\(__ : A) -> b);

let seq[A, B] (p : Parser[A]) (q : Parser[B]) : Parser[(A, B)] =
    bind[A, (A, B)] p (\(x : A) ->
    bind[B, (A, B)] q (\(y : B) ->
    result[(A, B)] (x, y)));

let choice[A] (p : Parser[A]) (q : Parser[A]) : Parser[A] =
    \(inp : PolyString) ->
        (p inp) +~[(A, PolyString)] (\(__ : Unit) -> q inp);

let (<|>)[A] (p : Parser[A]) (q : Parser[A]) : Parser[A] =
    choice[A] p q;

let sat (f : Char -> Bool) : Parser[Char] =
    bind[Char, Char] item (\(x : Char) -> if f x then (result[Char] x) else zero[Char]);

let char (x : Char) : Parser[Char] =
    sat (\(y : Char) -> x `charEq` y);

let notchar (x : Char) : Parser[Char] =
    sat (\(y : Char) -> x != y);

let digit : Parser[Char] =
    sat (\(x : Char) -> java.lang.Character.isDigit(x));

let upper : Parser[Char] =
    sat (\(x : Char) -> java.lang.Character.isUpperCase(x));

let lower : Parser[Char] =
    sat (\(x : Char) -> java.lang.Character.isLowerCase(x));

let space : Parser[Char] =
    sat (\(x : Char) -> java.lang.Character.isSpaceChar(x));

let whitespace : Parser[Char] =
    sat (\(x : Char) -> java.lang.Character.isWhitespace(x));

let letter : Parser[Char] =
    choice[Char] lower upper;

let alphanum : Parser[Char] =
    choice[Char] letter digit;

let noneof (s : PolyString) : Parser[Char] =
    sat (\(x : Char) -> (charin x s) `boolEq` False);

let oneof (s : PolyString) : Parser[Char] =
    sat (\(x : Char) -> charin x s);

let rec string (s : PolyString) : Parser[PolyString] =
    case s of
        Nil         ->  result[PolyString] (Nil[Char])
     |  Cons x xs   -> {
            let xs = invoke[PolyString] xs;
            (char x)
                >>[Char, PolyString] (string xs)
                $>[PolyString, PolyString] (x +>[Char] xs)
     };

-- Repetition

let rec many[A] (p : Parser[A]) : Parser[PolyList[A]] =
    choice[PolyList[A]] (bind[A, PolyList[A]] p (\(x : A) ->
                      bind[PolyList[A], PolyList[A]] (many[A] p) (\(xs : PolyList[A]) ->
                      result[PolyList[A]] (x +>[A] xs))))
                     (result[PolyList[A]] (Nil[A]));

let many1[A] (p : Parser[A]) : Parser[PolyList[A]] =
    bind[A, PolyList[A]] p (\(x : A) ->
    bind[PolyList[A], PolyList[A]] (many[A] p) (\(xs : PolyList[A]) ->
    result[PolyList[A]] (x +>[A] xs)));

let word : Parser[PolyString] =
    many1[Char] letter;

let ident : Parser[PolyString] =
    bind[Char, PolyString] lower (\(x : Char) ->
    bind[PolyString, PolyString] (many[Char] alphanum) (\(xs : PolyString) ->
    result[PolyString] (x +>[Char] xs)));

let natural : Parser[Int] = {
    let eval (xs : PolyList[Char]) =
        foldl[Int, Int] (\(b : Int) (a : Int) -> 10 * b + a)
                        0
                        (map[Char, Int] (\(c : Char) -> java.lang.Character.digit(c, 10)) xs);

    bind[PolyList[Char], Int] (many1[Char] digit) (\(xs : PolyList[Char]) -> result[Int] (eval xs))
};

let hexdecimal : Parser[Int] = {
    let eval (xs : PolyList[Char]) =
        foldl[Int, Int] (\(b : Int) (a : Int) -> 16 * b + a)
                        0
                        (map[Char, Int] (\(c : Char) -> java.lang.Character.digit(c, 16)) xs);
    (many1[Char] (oneof (fromString "1234567890abcdefABCDEF")))
        <$>[PolyList[Char], Int] (\(xs : PolyList[Char]) -> eval xs)
};

let int : Parser[Int] =
    choice[Int] ((char '-') >>[Char, Int]
                 (using[Int,Int] natural (\(n : Int) -> (-n))))
              natural;

-- Repetition with seperators

let sepby1[A, B] (p : Parser[A]) (sep : Parser[B]) : Parser[PolyList[A]] = {
    let rep[A, B] (p : Parser[A]) : Parser[A] =
        bind[B, A] sep (\(s : B) -> bind[A, A] p (\(y : A) -> result[A] y));
    bind[A, PolyList[A]] p (\(x : A) ->
    bind[PolyList[A], PolyList[A]] (many[A] (rep[A, B] p)) (\(xs : PolyList[A]) ->
    result[PolyList[A]] (Cons[A] x (\(__ : Unit) -> xs))))
};

let sepby[A, B] (p : Parser[A]) (sep : Parser[B]) : Parser[PolyList[A]] =
    choice[PolyList[A]] (sepby1[A, B] p sep) (result[PolyList[A]] (Nil[A]));


-- More complicated

-- Parses non-empty sequences of items seperated by operators that associate to the left
let chainl1[A] (p : Parser[A]) (op : Parser[A -> A -> A]) : Parser[A] = {
    let fy =
        bind[A -> A -> A, (A -> A -> A, A)] op (\(f : A -> A -> A) ->
        bind[A, (A -> A -> A, A)]           p  (\(y : A) ->
        result[(A -> A -> A, A)] (f, y)));

    bind[A, A]                           p                            (\(x : A) ->
    bind[PolyList[(A -> A -> A, A)], A]     (many[(A -> A -> A, A)] fy)  (\(fys: PolyList[(A -> A -> A, A)]) ->
    result[A] (foldl[(A -> A -> A, A), A] (\(x : A) (fy : (A -> A -> A, A)) -> fy._1 x fy._2) x fys)))
};

-- associate to the right
let rec chainr1[A] (p : Parser[A]) (op : Parser[A -> A -> A]) : Parser[A] =
    bind[A, A] p (\(x : A) ->
        choice[A] (bind[A -> A -> A, A] op (\(f : A -> A -> A) ->
                 bind[A, A] (chainr1[A] p op) (\(y : A) -> -- Calculate the result on the right
                 result[A] (f x y))))
                (result[A] x));

let chainl[A] (p : Parser[A]) (op : Parser[A -> A -> A]) (v : A) : Parser[A] =
    choice[A] (chainl1[A] p op) (result[A] v);

let chainr[A] (p : Parser[A]) (op : Parser[A -> A -> A]) (v : A) : Parser[A] =
    choice[A] (chainr1[A] p op) (result[A] v);


-- Improved Laziness (Haven't supported in F2J yet)

-- Pick the first (if exists) result
--let only[A] (p : Parser[A]) : Parser[A] =
--    \(inp : PolyString) ->
--        case (p inp) of
--            Nil         -> (Nil[(A, PolyString)])
--         |  Cons x xs   -> (singleton[(A, PolyString)] x);

let only[A] (p : Parser[A]) : Parser[A] =
    \(inp : PolyString) -> take[(A, PolyString)] 1 (p inp);

-- Simple arithmetic expression

let bracket[A, B, C] (open : Parser[A]) (p : Parser[B]) (close : Parser[C]) : Parser[B] =
    open *>[A, B] p <*[B, C] close;

let operation = {
    let addop (a : Int) (b : Int) : Int = a + b;
    let subop (a : Int) (b : Int) : Int = a - b;
    let mulop (a : Int) (b : Int) : Int = a * b;
    let divop (a : Int) (b : Int) : Int = a / b;

    let add = (char '+') $>[Char, Int -> Int -> Int] addop;
    let sub = (char '-') $>[Char, Int -> Int -> Int] subop;
    let mul = (char '*') $>[Char, Int -> Int -> Int] mulop;
    let div = (char '/') $>[Char, Int -> Int -> Int] divop;

    add `choice[Int -> Int -> Int]` sub `choice[Int -> Int -> Int]` mul `choice[Int -> Int -> Int]` div
};

let rec expr : Parser[Int] =
    \(s : PolyString) -> chainl1[Int] factor operation s
and factor : Parser[Int] =
    \(s : PolyString) -> choice[Int] natural (bracket[Char, Int, Char] (char '(') expr (char ')')) s;

-- Naive XML Parser

data XMLNode = XMLText      PolyString
             | XMLAttr      PolyString PolyString
             | XMLElement   PolyString PolyList[XMLNode] PolyList[XMLNode]
             | XMLCData     PolyString
             | XMLComment   PolyString
             | XMLProcInst  PolyString PolyList[XMLNode]
             ;

let rec xmlNodeEqual (a : XMLNode) (b : XMLNode) : Bool =
    case a of
        XMLText stra            -> {
            case b of
                XMLText strb                    -> polyStringEq stra strb
             |  XMLComment str                  -> False
             |  XMLAttr key val                 -> False
             |  XMLCData cdata                  -> False
             |  XMLProcInst t attrs             -> False
             |  XMLElement tag attrs children   -> False
        }
     |  XMLComment stra         -> {
            case b of
                XMLText strb                    -> False
             |  XMLComment strb                 -> polyStringEq stra strb
             |  XMLAttr key val                 -> False
             |  XMLCData cdata                  -> False
             |  XMLProcInst t attrs             -> False
             |  XMLElement tag attrs children   -> False
        }
     |  XMLAttr keya vala       -> {
            case b of
                XMLText strb                    -> False
             |  XMLComment strb                 -> False
             |  XMLAttr keyb valb               -> (polyStringEq keya keyb) && (polyStringEq vala valb)
             |  XMLCData cdata                  -> False
             |  XMLProcInst t attrs             -> False
             |  XMLElement tag attrs children   -> False
        }
     |  XMLCData cdataa         -> {
            case b of
                XMLText strb                    -> False
             |  XMLComment strb                 -> False
             |  XMLAttr key val                 -> False
             |  XMLCData cdatab                 -> polyStringEq cdataa cdatab
             |  XMLProcInst t attrs             -> False
             |  XMLElement tag attrs children   -> False
        }
     |  XMLProcInst ta attrsa   -> {
            case b of
                XMLText strb                    -> False
             |  XMLComment strb                 -> False
             |  XMLAttr key val                 -> False
             |  XMLCData cdatab                 -> False
             |  XMLProcInst tb attrsb           ->
                    ((polyStringEq ta tb) && (polyListEq[XMLNode] xmlNodeEqual attrsa attrsb))
             |  XMLElement tag attrs children   -> False
        }
     |  XMLElement taga attrsa childrena    -> {
            case b of
                XMLText strb                        -> False
             |  XMLComment str                      -> False
             |  XMLAttr key val                     -> False
             |  XMLCData cdata                      -> False
             |  XMLProcInst t attrs                 -> False
             |  XMLElement tagb attrsb childrenb    ->
                    (polyStringEq taga tagb)
                        && (polyListEq[XMLNode] xmlNodeEqual attrsa attrsb)
                        && (polyListEq[XMLNode] xmlNodeEqual childrena childrenb)
        };

let rec xmlNodeToString (xml : XMLNode) : String =
    case xml of
        XMLText str         -> "XMLText ".concat(toString str)
     |  XMLComment str      -> "XMLComment ".concat(toString str)
     |  XMLAttr key val     -> "XMLAttr ".concat(toString key).concat(" ").concat(toString val)
     |  XMLCData cdata      -> "XMLCData ".concat(toString cdata)
     |  XMLProcInst t attrs
            -> "XMLProcInst ".concat(toString t)
                             .concat(" ")
                             .concat(listToString[XMLNode] xmlNodeToString attrs)
     |  XMLElement tag attrs children
            -> "XMLElement ".concat(toString tag)
                            .concat(" ")
                            .concat(listToString[XMLNode] xmlNodeToString attrs)
                            .concat(" ")
                            .concat(listToString[XMLNode] xmlNodeToString children);

--let xmlComment : Parser[PolyList[Char]] =
--    bind[PolyString, PolyList[Char]]     (string (fromString "<!--")) (\(cstart : PolyString) ->
--    bind[PolyList[Char], PolyList[Char]] (many[Char] item) (\(comment : PolyList[Char]) ->
--    bind[PolyString, PolyList[Char]]     (string (fromString "-->")) (\(cend : PolyString) ->
--    result[PolyList[Char]] comment)));

let xmlComment : Parser[XMLNode] =
    (string (fromString "<!--"))
        *>[PolyString, PolyList[Char]] (many[Char] item)
        <*[PolyList[Char], PolyString] (string (fromString "-->"))
        <$>[PolyList[Char], XMLNode] (\(cmt : PolyList[Char]) -> XMLComment cmt);

--let xmlSpace : Parser[Unit] =
--    only[Unit]
--        (bind[PolyList[Char], Unit] (many[Char] whitespace) (\(sp1 : PolyList[Char]) ->
--         bind[PolyList[Char], Unit] xmlComment              (\(cmt : PolyList[Char]) ->
--         bind[PolyList[Char], Unit] (many[Char] whitespace) (\(sp2 : PolyList[Char]) ->
--         result[Unit] ()))));
let xmlSpace : Parser[Unit] =
    only[Unit]
        (choice[Unit]
            (bind[PolyList[Char], Unit] (many[Char] whitespace) (\(__ : PolyList[Char]) -> result[Unit] ()))
            (bind[PolyList[Char], Unit] (many[Char] whitespace) (\(sp1 : PolyList[Char]) ->
             bind[XMLNode, Unit]     xmlComment              (\(cmt : XMLNode) ->
             bind[PolyList[Char], Unit] (many[Char] whitespace) (\(sp2 : PolyList[Char]) ->
             result[Unit] ())))));

-- Normal version
let xmlEscapedChar : Parser[Char] = {
    let quot = (string (fromString "&quot;")) $>[PolyString, Char] '"';
    let apos = (string (fromString "&apos;")) $>[PolyString, Char] '\'';
    let lt = (string (fromString "&lt;"))     $>[PolyString, Char] '<';
    let gt = (string (fromString "&gt;"))     $>[PolyString, Char] '>';
    let amp = (string (fromString "&amp;"))   $>[PolyString, Char] '&';

    quot `choice[Char]` apos `choice[Char]` lt `choice[Char]` gt `choice[Char]` amp
};

let xmlEscapedCodePoint : Parser[PolyString] =
    (string (fromString "&#x"))
        *>[PolyString, Int] hexdecimal
        <*[Int, Char] (char ';')
        <$>[Int, PolyString] (\(codep : Int) ->
                (fromString (new java.lang.String(java.lang.Character.toChars(codep)))));

let xmlChar : Parser[Char] =
    xmlEscapedChar <|>[Char] (noneof (fromString "\"'<>&"));

let xmlString : Parser[PolyString] =
    many1[Char] xmlChar
       <|>[PolyString] xmlEscapedCodePoint;

let xmlDoubleQuotedString : Parser[PolyString] =
    (char '"') *>[Char, PolyString] xmlString <*[PolyString, Char] (char '"');

let xmlSingleQuotedString : Parser[PolyString] =
    (char '\'') *>[Char, PolyString] xmlString <*[PolyString, Char] (char '\'');

let xmlQuotedString : Parser[PolyString] =
    choice[PolyString] xmlDoubleQuotedString xmlSingleQuotedString;

let xmlText : Parser[XMLNode] =
    xmlString <$>[PolyString, XMLNode] (\(content : PolyString) -> XMLText content);

let xmlCData : Parser[XMLNode] =
    string (fromString "<![CDATA[")
        *>[PolyString, PolyList[Char]] (many[Char] item)
            <$>[PolyList[Char], XMLNode] (\(cd : PolyList[Char]) -> XMLCData cd)
        <*[XMLNode, PolyString] (string (fromString "]]>"));

let xmlKey : Parser[PolyString] =
    many1[Char] (letter <|>[Char] (char '-'));

let xmlAttr : Parser[XMLNode] =
    bind[PolyString, XMLNode]  xmlKey                                                          (\(key : PolyString) ->
    bind[PolyString, XMLNode]  (xmlSpace >>[Unit, Char] (char '=')
                                      >>[Char, Unit] xmlSpace
                                      >>[Unit, PolyString] xmlQuotedString)                    (\(val : PolyString) ->
    result[XMLNode] (XMLAttr key val)));

let xmlAttrs : Parser[PolyList[XMLNode]] =
    sepby[XMLNode, Unit] xmlAttr xmlSpace;

let xmlEndTag (tag : PolyString) : Parser[Unit] =
    (string (fromString "</"))
        >>[PolyString, Unit]   xmlSpace
        >>[Unit, PolyString]   (string tag)
        >>[PolyString, Unit]   xmlSpace
        >>[Unit, Char]      (char '>')
        $>[Char, Unit]      ();

--let rec xmlElement : Parser[XMLNode] =
--    bind[Char, XMLNode]             (char '<')                                  (\(__ : Char) ->
--    bind[Unit, XMLNode]             xmlSpace                                    (\(__ : Unit) ->
--    bind[PolyString, XMLNode]          xmlKey                                      (\(tag : PolyString) ->
--    bind[Unit, XMLNode]             xmlSpace                                    (\(__ : Unit) ->
--    bind[PolyList[XMLNode], XMLNode]   xmlAttrs                                    (\(attrs: PolyList[XMLNode]) ->
--    bind[Unit, XMLNode]             xmlSpace                                    (\(__ : Unit) ->
--        (bind[Char, XMLNode]             (char '>')                             (\(__ : Char) ->
--         bind[Unit, XMLNode]             xmlSpace                               (\(__ : Unit) ->
--         bind[PolyList[XMLNode], XMLNode]   xmlElementChildren                     (\(ch : PolyList[XMLNode]) ->
--         bind[Unit, XMLNode]             xmlSpace                               (\(__ : Unit) ->
--         bind[Unit, XMLNode]             (xmlEndTag tag)                        (\(__ : Unit) ->
--         result[XMLNode] (XMLElement tag attrs ch)))))))
--
--        `choice[XMLNode]` -- ^~~ normal style  v~~ shorter style
--
--        ((string (fromString "/>")) <$>[PolyString, XMLNode] (\(__ : PolyString) -> (XMLElement tag attrs (Nil[XMLNode]))))
--    ))))))

let rec xmlElement : Parser[XMLNode] =
    (char '<') >>[Char, Unit] xmlSpace >>[Unit, PolyString] xmlKey >>=[PolyString, XMLNode] (\(tag : PolyString) ->
        xmlSpace >>[Unit, PolyList[XMLNode]] xmlAttrs >>=[PolyList[XMLNode], XMLNode] (\(attrs : PolyList[XMLNode]) ->
            -- Normal ends
            ((char '>') >>[Char, Unit] xmlSpace >>[Unit, PolyList[XMLNode]]
                xmlElementChildren <*[PolyList[XMLNode], Unit] xmlSpace <*[PolyList[XMLNode], Unit] (xmlEndTag tag)
                    <$>[PolyList[XMLNode], XMLNode] (\(ch : PolyList[XMLNode]) -> XMLElement tag attrs ch))

            <|>[XMLNode]

            -- Short ends
            ((string (fromString "/>"))
                >>[PolyString, XMLNode] (result[XMLNode] (XMLElement tag attrs (Nil[XMLNode]))))))

and xmlElementChildren : Parser[PolyList[XMLNode]] =
    (xmlCData <$>[XMLNode, PolyList[XMLNode]] (\(c : XMLNode) -> singleton[XMLNode] c)
        <|>[PolyList[XMLNode]] (xmlText `using[XMLNode, PolyList[XMLNode]]` (\(n : XMLNode) -> singleton[XMLNode] n))
        <|>[PolyList[XMLNode]] (sepby1[XMLNode, Unit] xmlElement xmlSpace))
    <|>[PolyList[XMLNode]] (result[PolyList[XMLNode]] (Nil[XMLNode]));

let xmlProcInst : Parser[XMLNode] =
    (string (fromString "<?")) >>[PolyString, Unit] xmlSpace
        >>[Unit, PolyString] xmlKey >>=[PolyString, XMLNode] (\(target : PolyString) ->
            xmlSpace >>[Unit, PolyList[XMLNode]] xmlAttrs
                <*[PolyList[XMLNode], PolyString] (xmlSpace >>[Unit, PolyString] (string (fromString "?>")))
                    >>=[PolyList[XMLNode], XMLNode] (\(conts : PolyList[XMLNode]) ->
                        result[XMLNode] (XMLProcInst target conts)));

let parseXML : Parser[PolyList[XMLNode]] =
    (many[XMLNode] xmlProcInst)
        >>=[PolyList[XMLNode], PolyList[XMLNode]] (\(procinst1 : PolyList[XMLNode]) ->
            xmlElement >>=[XMLNode, PolyList[XMLNode]] (\(root : XMLNode) ->
                (many[XMLNode] xmlProcInst) >>=[PolyList[XMLNode], PolyList[XMLNode]] (\(procinst2 : PolyList[XMLNode]) ->
                    result[PolyList[XMLNode]] (procinst1 ++[XMLNode] (root +>[XMLNode] procinst2)))));


-- Test

let println (s : String) : Unit = { java.lang.System.out.println(s); () };
let print (s : String) : Unit = { java.lang.System.out.print(s); () };

type TestFn = {
    name : String,
    fn   : Unit -> Unit
};

type TestSuite = {
    name : String,
    fns  : PolyList[TestFn]
};

let runTest (test : TestFn) : Unit = {
    let testingPrefix : String = "\u001b[31;1mtesting\u001b[0m\t";
    println testingPrefix.concat(test.name).concat(" :");
    test.fn ();
    println "\t...ok"
};

let runTests (tests : PolyList[TestFn]) : Unit =
    foldl[TestFn, Unit]
        (\(__ : Unit) (fn : TestFn) -> runTest fn) () tests;

let runTestSuite (suite : TestSuite) : Unit = {
    println "\n\t\u001b[32;1mRunning\u001b[37;1m ".concat(suite.name).concat("\u001b[0m ...\n");
    println "Running ".concat((len[TestFn] suite.fns).toString()).concat(" tests");
    runTests suite.fns
};

let runTestSuites (suites : PolyList[TestSuite]) : Unit =
    foldl[TestSuite, Unit]
        (\(__ : Unit) (suite : TestSuite) -> runTestSuite suite) () suites;

let error (msg : String) = {
    java.lang.System.err.println(msg);
    java.lang.System.exit(1);
    ()
};

-- Test helper functions

let assert (b : Bool) =
    if b then
        ()
    else
        error "assert failed!";

let printFirstXMLResult (r : PolyList[(XMLNode, PolyString)]) =
    case first[(XMLNode, PolyString)] r of
        Nothing        -> print "Nothing in the List!!"
     |  Just x         -> print "(".concat(xmlNodeToString x._1).concat(", \"").concat(toString x._2).concat("\")");

let firstParseResultEqual[A] (eq : Eq[A]) (r : PolyList[(A, PolyString)]) (expected : (A, PolyString)) : Bool =
    case first[(A, PolyString)] r of
        Nothing         -> False
     |  Just x          -> (eq x._1 expected._1) && (polyStringEq x._2 expected._2);

-- Test utilities

let testListEqual : TestFn = {
    name = "polyListEq",
    fn   = \(__ : Unit) -> assert (polyListEq[Int]
                                        intEq
                                        (singleton[Int] 1)
                                        (singleton[Int] 1))
};

let testMap : TestFn = {
    name = "map",
    fn   = \(__ : Unit) ->
        assert (polyListEq[Char]
                    charEq
                    (map[Char, Char] (\(c : Char) -> 'm') (fromString "hello"))
                    (fromString "mmmmm"))
};

let testFoldr : TestFn = {
    name = "foldr",
    fn   = \(__ : Unit) ->
        assert ((foldr[Int,Int] (\(a : Int) (b : Int) -> a + b) 0 (take[Int] 5 (nats 1))) `intEq` 15)
};

let testFoldl : TestFn = {
    name = "foldl",
    fn   = \(__ : Unit) ->
        assert ((foldl[Int,Int] (\(b : Int) (a : Int) -> a + b) 0 (take[Int] 5 (nats 1))) `intEq` 15)
};

let testUtilitiesSuite : TestSuite = {
    name = "Test Utilities",
    fns  = testListEqual
                +>[TestFn] (testMap
                +>[TestFn] (testFoldr
                +>[TestFn] (testFoldl
                +>[TestFn] (Nil[TestFn]))))
};

-- Test combinators

let testChar : TestFn = {
    name = "char 'a' (fromString \"abcd\")",
    fn   = \(__ : Unit) -> assert (parseResultListEq[Char]
                                      charEq
                                      (char 'a' (fromString "abcd"))
                                      (singleton[(Char, PolyString)] ('a', (fromString "bcd"))))
};

let testUpper : TestFn = {
    name = "upper (fromString \"ABC\")",
    fn   = \(__ : Unit) -> assert (parseResultListEq[Char]
                                      charEq
                                      (upper (fromString "ABC"))
                                      (singleton[(Char, PolyString)] ('A', (fromString "BC"))))
};

let testLetter : TestFn = {
    name = "letter (fromString \"abcd\")",
    fn   = \(__ : Unit) -> assert (parseResultListEq[Char]
                                      charEq
                                      (letter (fromString "abcd"))
                                      (singleton[(Char, PolyString)] ('a', (fromString "bcd"))))
};

let testString : TestFn = {
    name = "string (fromString \"abc\") (fromString \"abcd\")",
    fn   = \(__ : Unit) -> assert (parseResultListEq[PolyString]
                                        polyStringEq
                                        (string (fromString "abc") (fromString "abcd"))
                                        (singleton[(PolyString, PolyString)] ((fromString "abc"), (fromString "d"))))
};

let testMany : TestFn = {
    name = "many (char 'a') (fromString \"aaab\")",
    fn   = \(__ : Unit) -> {
        let expected = (fromString "aaa", fromString "b")
            +>[(PolyString, PolyString)] ((fromString "aa", fromString "ab")
            +>[(PolyString, PolyString)] ((fromString "a", fromString "aab")
            +>[(PolyString, PolyString)] ((fromString "", fromString "aaab")
            +>[(PolyString, PolyString)] (Nil[(PolyString, PolyString)]))));
        assert (parseResultListEq[PolyString]
                    polyStringEq
                    (many[Char] (char 'a') (fromString "aaab"))
                    expected)
    }
};

let testMany1 : TestFn = {
    name = "many1 (char 'a') (fromString \"aaab\")",
    fn   = \(__ : Unit) -> {
        let expected = (fromString "aaa", fromString "b")
            +>[(PolyString, PolyString)] ((fromString "aa", fromString "ab")
            +>[(PolyString, PolyString)] ((fromString "a", fromString "aab")
            +>[(PolyString, PolyString)] (Nil[(PolyString, PolyString)])));
        assert (parseResultListEq[PolyString]
                    polyStringEq
                    (many1[Char] (char 'a') (fromString "aaab"))
                    expected)
    }
};

let testChoice : TestFn = {
    name = "choice (char 'a') (char 'b') (fromString \"aaab\")",
    fn   = \(__ : Unit) ->
        assert (parseResultListEq[Char]
                    charEq
                    (choice[Char] (char 'a') (char 'b') (fromString "aaab"))
                    (singleton[(Char, PolyString)] ('a', fromString "aab")))
};

let testChoice2 : TestFn = {
    name = "choice (string \"aa\") (string \"aaa\") (fromString \"aaab\")",
    fn   = \(__ : Unit) -> {
        let expected = (fromString "aa", fromString "ab")
            +>[(PolyString, PolyString)] ((fromString "aaa", fromString "b")
            +>[(PolyString, PolyString)] (Nil[(PolyString, PolyString)]));

        assert (parseResultListEq[PolyString]
                    polyStringEq
                    (choice[PolyString] (string (fromString "aa")) (string (fromString "aaa")) (fromString "aaab"))
                    expected)
    }
};

let testWord : TestFn = {
    name = "word (fromString \"aaabcd abcd\")",
    fn   = \(__ : Unit) -> {
        let expected = (fromString "aaabcd", fromString " abcd")
            +>[(PolyString, PolyString)] ((fromString "aaabc", fromString "d abcd")
            +>[(PolyString, PolyString)] ((fromString "aaab", fromString "cd abcd")
            +>[(PolyString, PolyString)] ((fromString "aaa", fromString "bcd abcd")
            +>[(PolyString, PolyString)] ((fromString "aa", fromString "abcd abcd")
            +>[(PolyString, PolyString)] ((fromString "a", fromString "aabcd abcd")
            +>[(PolyString, PolyString)] (Nil[(PolyString, PolyString)]))))));
        assert (parseResultListEq[PolyString]
                    polyStringEq
                    (word (fromString "aaabcd abcd"))
                    expected)
    }
};

let testNatural : TestFn = {
    name = "natural (fromString \"12345\")",
    fn   = \(__ : Unit) -> {
        let expected = (12345, fromString "")
            +>[(Int, PolyString)] ((1234, fromString "5")
            +>[(Int, PolyString)] ((123, fromString "45")
            +>[(Int, PolyString)] ((12, fromString "345")
            +>[(Int, PolyString)] ((1, fromString "2345")
            +>[(Int, PolyString)] (Nil[(Int, PolyString)])))));

        assert (parseResultListEq[Int]
                    intEq
                    (natural (fromString "12345"))
                    expected)
    }
};

let testHexDecimal : TestFn = {
    name = "hexdecimal (fromString \"a0\")",
    fn   = \(__ : Unit) ->
         assert (firstParseResultEqual[Int]
                    intEq
                    (hexdecimal (fromString "a0"))
                    (160, fromString ""))
};

let testSepby1 : TestFn = {
    name = "sepby1[PolyString, Char] word (char ',') (fromString \"abc,def\")",
    fn   = \(__ : Unit) ->
        assert (firstParseResultEqual[PolyList[PolyString]]
                    (polyListEq[PolyString] polyStringEq)
                    (sepby1[PolyString, Char] (word) (char ',') (fromString "abc,def"))
                    ((fromString "abc") +>[PolyString] ((fromString "def") +>[PolyString] (Nil[PolyString])),
                     (fromString "")))
};

let testNoneOf : TestFn = {
    name = "noneof (fromString \"abc\") (fromString \"dce\")",
    fn   = \(__ : Unit) ->
        assert (firstParseResultEqual[Char]
                    charEq
                    (noneof (fromString "abc") (fromString "dce"))
                    ('d', fromString "ce"))
};

let testCombinatorsSuite : TestSuite = {
    name = "Test Combinators",
    fns  = testChar
                +>[TestFn] (testUpper
                +>[TestFn] (testLetter
                +>[TestFn] (testString
                +>[TestFn] (testMany
                +>[TestFn] (testMany1
                +>[TestFn] (testChoice
                +>[TestFn] (testChoice2
                +>[TestFn] (testWord
                +>[TestFn] (testNatural
                +>[TestFn] (testHexDecimal
                +>[TestFn] (testSepby1
                +>[TestFn] (Nil[TestFn]))))))))))))
};

-- Test Simple ARithmetic Expression Parser

let testSimpleExpr : TestFn = {
    name = "expr (fromString \"123\")",
    fn   = \(__ : Unit) ->
        assert (firstParseResultEqual[Int]
                    intEq
                    (expr (fromString "123"))
                    (123, fromString ""))
};

let testSimpleExprWithBracket : TestFn = {
    name = "expr (fromString \"(123)\")",
    fn   = \(__ : Unit) ->
        assert (firstParseResultEqual[Int]
                    intEq
                    (expr (fromString "(123)"))
                    (123, fromString ""))
};

let testSimpleExpr2 : TestFn = {
    name = "expr (fromString \"1+2\")",
    fn   = \(__ : Unit) ->
        assert (firstParseResultEqual[Int]
                    intEq
                    (expr (fromString "1+2"))
                    (3, fromString ""))
};

let testSimpleExpr3 : TestFn = {
    name = "expr (fromString \"10+2\")",
    fn   = \(__ : Unit) ->
        assert (firstParseResultEqual[Int]
                    intEq
                    (expr (fromString "10+2"))
                    (12, fromString ""))
};

let testSimpleExpr4 : TestFn = {
    name = "expr (fromString \"(10+2)*2\")",
    fn   = \(__ : Unit) ->
        assert (firstParseResultEqual[Int]
                    intEq
                    (expr (fromString "(10+2)*2"))
                    (24, fromString ""))
};

let testSimpleExpr5 : TestFn = {
    name = "expr (fromString \"(10+2)*2/6\")",
    fn   = \(__ : Unit) ->
        assert (firstParseResultEqual[Int]
                    intEq
                    (expr (fromString "(10+2)*2/6"))
                    (4, fromString ""))
};

let testSimpleArithmetricExprSuite : TestSuite = {
    name = "Test Simple Arithmetic Suite",
    fns  = testSimpleExpr
                +>[TestFn] (testSimpleExprWithBracket
                +>[TestFn] (testSimpleExpr2
                +>[TestFn] (testSimpleExpr3
                +>[TestFn] (testSimpleExpr4
                +>[TestFn] (testSimpleExpr5
                +>[TestFn] (Nil[TestFn]))))))
};

-- Test Naive XML Parser

let testXMLComment : TestFn = {
    name = "xmlComment (fromString \"<!-- hahaa? -->\")",
    fn   = \(__ : Unit) ->
        assert (firstParseResultEqual[XMLNode]
                    xmlNodeEqual
                    (xmlComment (fromString "<!-- hahaa? -->"))
                    ((XMLComment (fromString " hahaa? ")), fromString ""))
};

let testXMLDoubleQuotedString : TestFn = {
    name = "xmlDoubleQuotedString (fromString \"\\\"abcdef\\\"\")",
    fn   = \(__ : Unit) ->
        assert (firstParseResultEqual[PolyString]
                    polyStringEq
                    (xmlDoubleQuotedString (fromString "\"abcdef\""))
                    (fromString "abcdef", fromString ""))
};

let testXMLSingleQuotedString : TestFn = {
    name = "xmlSingleQuotedString (fromString \"'abcdef'\")",
    fn   = \(__ : Unit) ->
        assert (firstParseResultEqual[PolyString]
                    polyStringEq
                    (xmlSingleQuotedString (fromString "'abcdef'"))
                    ((fromString "abcdef"), (fromString "")))
};

let testXMLText : TestFn = {
    name = "xmlText (fromString \"abcdefg&lt;hijk\")",
    fn   = \(__ : Unit) ->
        assert (firstParseResultEqual[XMLNode]
                    xmlNodeEqual
                    (xmlText (fromString "abcdefg&lt;hijk"))
                    (XMLText (fromString "abcdefg<hijk"), fromString ""))
};

let testXMLTextEmpty : TestFn = {
    name = "xmlText (fromString \"\")",
    fn   = \(__ : Unit) ->
        assert (isempty[(XMLNode, PolyString)] (xmlText (fromString "")))
};

let testXMLAttr : TestFn = {
    name = "xmlAttr (fromString \"key = \"vvvalue\"\")",
    fn   = \(__ : Unit) ->
        assert (firstParseResultEqual[XMLNode]
                    xmlNodeEqual
                    (xmlAttr (fromString "key = \"vvvalue\""))
                    (XMLAttr (fromString "key") (fromString "vvvalue"), fromString ""))
};

let testXMLAttrs : TestFn = {
    name = "xmlAttrs (fromString \"key = \\\"vvvalue\\\" hello = \\\"world\\\" haha='what?'\")",
    fn   = \(__ : Unit) ->
        assert (firstParseResultEqual[PolyList[XMLNode]]
                    (polyListEq[XMLNode] xmlNodeEqual)
                    (xmlAttrs (fromString "key = \"vvvalue\" hello = \"world\" haha='what?'"))
                    ((XMLAttr (fromString "key") (fromString "vvvalue"))
                        +>[XMLNode] ((XMLAttr (fromString "hello") (fromString "world"))
                        +>[XMLNode] ((XMLAttr (fromString "haha") (fromString "what?"))
                        +>[XMLNode] (Nil[XMLNode]))),
                     fromString ""))
};

let testElementSimple : TestFn = {
    name = "xmlElement (fromString \"<a></a>\")",
    fn   = \(__ : Unit) ->
        assert (firstParseResultEqual[XMLNode]
                    xmlNodeEqual
                    (xmlElement (fromString "<a></a>"))
                    (XMLElement (fromString "a") (Nil[XMLNode]) (Nil[XMLNode]), fromString ""))
};

let testElementWithText : TestFn = {
    name = "xmlElement (fromString \"<a>ha?haha!</a>\")",
    fn   = \(__ : Unit) ->
        assert (firstParseResultEqual[XMLNode]
                    xmlNodeEqual
                    (xmlElement (fromString "<a>ha?haha!</a>"))
                    (XMLElement (fromString "a") (Nil[XMLNode]) (singleton[XMLNode] (XMLText (fromString "ha?haha!"))),
                     fromString ""))
};

let testElementWithChildren : TestFn = {
    name = "xmlElement (fromString \"<a><b></b></a>\")",
    fn   = \(__ : Unit) ->
        assert (firstParseResultEqual[XMLNode]
                    xmlNodeEqual
                    (xmlElement (fromString "<a><b></b></a>"))
                    (XMLElement (fromString "a") (Nil[XMLNode])
                        (singleton[XMLNode] (XMLElement (fromString "b") (Nil[XMLNode]) (Nil[XMLNode]))),
                     fromString ""))
};

let testElementWithChildren2 : TestFn = {
    name = "xmlElement (fromString \"<a><b></b><c></c></a>\")",
    fn   = \(__ : Unit) -> {
        let b = XMLElement (fromString "b") (Nil[XMLNode]) (Nil[XMLNode]);
        let c = XMLElement (fromString "c") (Nil[XMLNode]) (Nil[XMLNode]);
        let a = XMLElement (fromString "a") (Nil[XMLNode]) (b +>[XMLNode] (c +>[XMLNode] (Nil[XMLNode])));
        assert (firstParseResultEqual[XMLNode]
                    xmlNodeEqual
                    (xmlElement (fromString "<a><b></b><c></c></a>"))
                    (a, fromString ""))
    }
};

let testElementWithAttr : TestFn = {
    name = "xmlElement (fromString \"<a href='/toosimple/sometimes/naive'>ha?haha!</a>\")",
    fn   = \(__ : Unit) -> {
        let elem = XMLElement
                        (fromString "a")
                        (singleton[XMLNode] (XMLAttr (fromString "href") (fromString "/toosimple/sometimes/naive")))
                        (singleton[XMLNode] (XMLText (fromString "ha?haha!")));
        assert (firstParseResultEqual[XMLNode]
                    xmlNodeEqual
                    (xmlElement (fromString "<a href='/toosimple/sometimes/naive'>ha?haha!</a>"))
                    (elem, fromString ""))
    }
};

let testElementWithAttrWithChildren : TestFn = {
    name = "xmlElement (fromString \"<a href='/toosimple/sometimes/naive'><b></b></a>\")",
    fn   = \(__ : Unit) -> {
        let elem = XMLElement
                        (fromString "a")
                        (singleton[XMLNode] (XMLAttr (fromString "href") (fromString "/toosimple/sometimes/naive")))
                        (singleton[XMLNode] (XMLElement (fromString "b") (Nil[XMLNode]) (Nil[XMLNode])));
        assert (firstParseResultEqual[XMLNode]
                    xmlNodeEqual
                    (xmlElement (fromString "<a href='/toosimple/sometimes/naive'><b></b></a>"))
                    (elem, fromString ""))
    }
};

let testElementSimple2 : TestFn = {
    name = "xmlElement (fromString \"<you young='too' simple='too'><naive>true</naive></you>\")",
    fn   = \(__ : Unit) -> {
        let elem = XMLElement
                        (fromString "you")
                        ((XMLAttr (fromString "young") (fromString "too"))
                            +>[XMLNode] ((XMLAttr (fromString "simple") (fromString "too"))
                            +>[XMLNode] (Nil[XMLNode])))
                        (singleton[XMLNode] (XMLElement (fromString "naive") (Nil[XMLNode])
                            (singleton[XMLNode] (XMLText (fromString "true")))));
        assert (firstParseResultEqual[XMLNode]
                    xmlNodeEqual
                    (xmlElement
                        (fromString
                            "<you young='too' simple='too'><naive>true</naive></you>"))
                    (elem, fromString ""))
    }
};

let testElementSimpleEndTag : TestFn = {
    name = "xmlElement (fromString \"<a/>\")",
    fn   = \(__ : Unit) -> {
        let elem = XMLElement (fromString "a") (Nil[XMLNode]) (Nil[XMLNode]);
        assert (firstParseResultEqual[XMLNode]
                    xmlNodeEqual
                    (xmlElement (fromString "<a/>"))
                    (elem, fromString ""))
    }
};

let testElementSimpleEndTag2 : TestFn = {
    name = "xmlElement (fromString \"<you young='too' simple='too'><naive><required/></naive></you>\")",
    fn   = \(__ : Unit) -> {
        let elem = XMLElement
                        (fromString "you")
                        ((XMLAttr (fromString "young") (fromString "too"))
                            +>[XMLNode] ((XMLAttr (fromString "simple") (fromString "too"))
                            +>[XMLNode] (Nil[XMLNode])))
                        (singleton[XMLNode] (XMLElement (fromString "naive") (Nil[XMLNode])
                            (singleton[XMLNode] (XMLElement (fromString "required") (Nil[XMLNode]) (Nil[XMLNode])))));
        assert (firstParseResultEqual[XMLNode]
                    xmlNodeEqual
                    (xmlElement
                        (fromString
                            "<you young='too' simple='too'><naive><required/></naive></you>"))
                    (elem, fromString ""))
    }
};

let testElementSimpleEndTag3 : TestFn = {
    name = "xmlElement (fromString \"<you young='too' simple='too'>\\n\\t<naive>\\n\\t\\t<required/>\\n\\t</naive>\\n</you>\")",
    fn   = \(__ : Unit) -> {
        let elem = XMLElement
                        (fromString "you")
                        ((XMLAttr (fromString "young") (fromString "too"))
                            +>[XMLNode] ((XMLAttr (fromString "simple") (fromString "too"))
                            +>[XMLNode] (Nil[XMLNode])))
                        (singleton[XMLNode] (XMLElement (fromString "naive") (Nil[XMLNode])
                            (singleton[XMLNode] (XMLElement (fromString "required") (Nil[XMLNode]) (Nil[XMLNode])))));

        assert (firstParseResultEqual[XMLNode]
                    xmlNodeEqual
                    (xmlElement
                        (fromString
                            "<you young='too' simple='too'>\n\t<naive>\n\t\t<required/>\n\t</naive>\n</you>"))
                    (elem, fromString ""))
    }
};

let testElementSimpleEndTag4 : TestFn = {
    name = "xmlElement (fromString \"<a href='https://github.com/hkuplg/fcore'/>\")",
    fn   = \(__ : Unit) -> {
        let elem = XMLElement
                        (fromString "a")
                        (singleton[XMLNode] (XMLAttr (fromString "href")
                                                     (fromString "https://github.com/hkuplg/fcore")))
                        (Nil[XMLNode]);
        assert (firstParseResultEqual[XMLNode]
                    xmlNodeEqual
                    (xmlElement (fromString "<a href='https://github.com/hkuplg/fcore'/>"))
                    (elem, fromString ""))
    }
};

let testCDataSimple : TestFn = {
    name = "xmlCData (fromString \"<![CDATA[function matchwo(a,b) { if (a < b && a < 0) then }]]>\"",
    fn   = \(__ : Unit) -> {
        let elem = XMLCData (fromString "function matchwo(a,b) { if (a < b && a < 0) then }");
        assert (firstParseResultEqual[XMLNode]
                    xmlNodeEqual
                    (xmlCData (fromString "<![CDATA[function matchwo(a,b) { if (a < b && a < 0) then }]]>"))
                    (elem, fromString ""))
    }
};

let testElementWithCData : TestFn = {
    name = "xmlElement (fromString \"<script><![CDATA[function matchwo(a,b) { &&<>!!!?; }]]></script>\"",
    fn   = \(__ : Unit) -> {
        let elem = XMLElement (fromString "script") (Nil[XMLNode])
                        (singleton[XMLNode] (XMLCData (fromString "function matchwo(a,b) { &&<>!!!?; }")));
        assert (firstParseResultEqual[XMLNode]
                    xmlNodeEqual
                    (xmlElement (fromString "<script><![CDATA[function matchwo(a,b) { &&<>!!!?; }]]></script>"))
                    (elem, fromString ""))
    }
};

let testProcInst : TestFn = {
    name = "xmlProcInst (fromString \"<?xml version=\\\"1.0\\\" encoding=\\\"UTF-8\\\" ?>\")",
    fn   = \(__ : Unit) -> {
        let elem = XMLProcInst (fromString "xml")
                        ((XMLAttr (fromString "version") (fromString "1.0"))
                            +>[XMLNode] ((XMLAttr (fromString "encoding") (fromString "UTF-8"))
                            +>[XMLNode] (Nil[XMLNode])));
        assert (firstParseResultEqual[XMLNode]
                    xmlNodeEqual
                    (xmlProcInst (fromString "<?xml version=\"1.0\" encoding=\"UTF-8\" ?>"))
                    (elem, fromString ""))
    }
};

let testProcInst2 : TestFn = {
    name = "xmlProcInst (fromString \"<?xml-stylesheet ?>\")",
    fn   = \(__ : Unit) -> {
        let elem = XMLProcInst (fromString "xml-stylesheet") (Nil[XMLNode]);
        assert (firstParseResultEqual[XMLNode]
                    xmlNodeEqual
                    (xmlProcInst (fromString "<?xml-stylesheet ?>"))
                    (elem, fromString ""))
    }
};

let testNaiveXMLParserSuite : TestSuite = {
    name = "Test Naive XML Parser",
    fns  = testXMLComment
                +>[TestFn] (testXMLDoubleQuotedString
                +>[TestFn] (testXMLSingleQuotedString
                +>[TestFn] (testXMLText
                +>[TestFn] (testXMLTextEmpty
                +>[TestFn] (testXMLAttr
                +>[TestFn] (testXMLAttrs
                +>[TestFn] (testElementSimple
                +>[TestFn] (testElementWithText
                +>[TestFn] (testElementWithChildren
                +>[TestFn] (testElementWithChildren2
                +>[TestFn] (testElementWithAttr
                +>[TestFn] (testElementWithAttrWithChildren
                +>[TestFn] (testElementSimple2
                +>[TestFn] (testElementSimpleEndTag
                +>[TestFn] (testElementSimpleEndTag2
                +>[TestFn] (testElementSimpleEndTag3
                +>[TestFn] (testElementSimpleEndTag4
                +>[TestFn] (testCDataSimple
                +>[TestFn] (testElementWithCData
                +>[TestFn] (testProcInst
                +>[TestFn] (testProcInst2
                +>[TestFn] (Nil[TestFn]))))))))))))))))))))))
};

let parserTestSuites : PolyList[TestSuite] =
    testUtilitiesSuite
        +>[TestSuite] (testCombinatorsSuite
        +>[TestSuite] (testSimpleArithmetricExprSuite
        +>[TestSuite] (testNaiveXMLParserSuite
        +>[TestSuite] (Nil[TestSuite]))));

{
    runTestSuites parserTestSuites;

    "\n-- END --"
}
